plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

publishing {
    repositories {
        maven {
            url = "$buildDir/repo"
        }
    }
}

tasks.withType(Copy).all {
    duplicatesStrategy 'exclude'
}

group 'io.github.takiviko.guide-generator'
version '1.0.0-002'

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.24'
    reflectionsVersion = '0.10.2'
    flexMarkVersion = '0.64.0'
    jUnitVersion = '5.9.0'
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.reflections:reflections:${reflectionsVersion}"
    implementation "com.vladsch.flexmark:flexmark-pdf-converter:${flexMarkVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${jUnitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jUnitVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation gradleApi()
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName.set('guide-generator-shadowed')
    archiveClassifier.set('')
    archiveVersion.set('')
}

gradlePlugin {
    website = 'https://github.com/takiviko/guide-generator/'
    vcsUrl = 'https://github.com/takiviko/guide-generator/'
    plugins {
        guide {
            id = "io.github.takiviko.guide-generator"
            implementationClass = "takiviko.guidegenerator.plugin.GuideGeneratorPlugin"
            displayName = "Guide generator plugin"
            description = "Generates user docs from package-level annotations"
            tags.set(['test', 'guide', 'documentation', 'pdf', 'markdown', 'util', 'generate'])
        }
    }
}